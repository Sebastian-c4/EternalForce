DEFINE_ACTION_FUNCTION armor_and_sabre BEGIN

	/* 剑型 */
	LAF CREATE_EFFECT INT_VAR opcode=187 timing=9 parameter1=1 STR_VAR name=c4efPF10 variable=~c4EF_Weapon_Proficiency~ END
	LAF CREATE_EMPTY_SPELL STR_VAR name=c4efPF10 END
	COPY_EXISTING c4efPF10.spl override
		LPF ADD_SPELL_EFFECT INT_VAR opcode=73 target=2 timing=9 parameter1=2 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 timing=9 parameter2=2 STR_VAR resource=c4efPF10 END
		
	LAF CREATE_EFFECT INT_VAR opcode=187 timing=9 parameter1=2 STR_VAR name=c4efPF20 variable=~c4EF_Weapon_Proficiency~ END
	LAF CREATE_EMPTY_SPELL STR_VAR name=c4efPF20 END
	COPY_EXISTING c4efPF20.spl override
		LPF ADD_SPELL_EFFECT INT_VAR opcode=73 target=2 timing=9 parameter1=1 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=278 target=2 timing=9 parameter1=1 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 timing=9 parameter2=2 STR_VAR resource=c4efPF20 END
		
	LAF CREATE_EFFECT INT_VAR opcode=187 timing=9 parameter1=3 STR_VAR name=c4efPF30 variable=~c4EF_Weapon_Proficiency~ END
	LAF CREATE_EMPTY_SPELL STR_VAR name=c4efPF30 END
	COPY_EXISTING c4efPF30.spl override
		LPF ADD_SPELL_EFFECT INT_VAR opcode=278 target=2 timing=9 parameter1=2 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 timing=9 parameter2=2 STR_VAR resource=c4efPF30 END
		
	//标注高阶表
	COPY_EXISTING_REGEXP GLOB ~c4efPF[1-3]0.spl~ override
		LPF ADD_SPELL_EFFECT INT_VAR opcode=313 target=2 timing=9 STR_VAR resource=~%SOURCE_RES%~ END

	/* 双手 */
	LAF CREATE_EMPTY_SPELL STR_VAR name=c4efPF11 END
	COPY_EXISTING c4efPF11.spl override
		LPF ALTER_SPELL_HEADER INT_VAR target=5 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 STR_VAR resource=~%SOURCE_RES%~ END
		LPF CLONE_EFFECT INT_VAR match_opcode=321 opcode=142 duration=6 parameter2=148 STR_VAR resource=~~ insert=last END
		PATCH_FOR_EACH type IN 1 5 7 BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=321 opcode=332 duration=6 parameter2=type STR_VAR resource=~~ insert=last END
		END
		LPF CREATE_HEADERS_AS_LEVEL END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=332 parameter1=33 multi_parameter1=1 END
		
	//精通
	LAF CREATE_EMPTY_SPELL STR_VAR name=c4efPF12 END
	COPY_EXISTING c4efPF12.spl override
		LPF ALTER_SPELL_HEADER INT_VAR target=5 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 STR_VAR resource=~%SOURCE_RES%~ END
		LPF CLONE_EFFECT INT_VAR match_opcode=321 opcode=142 duration=6 parameter2=148 STR_VAR resource=~~ insert=last END
		PATCH_FOR_EACH type IN 1 5 7 BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=321 opcode=332 duration=6 parameter2=type STR_VAR resource=~~ insert=last END
		END
		LPF CREATE_HEADERS_AS_LEVEL END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=332 parameter1=66 multi_parameter1=1 END

	//命中后移除，放在护甲
	LAF CREATE_EFFECT INT_VAR opcode=146 target=2 parameter2=1 STR_VAR name=c4efPF15 resource=c4efPF15 END
	LAF CREATE_EFFECT INT_VAR opcode=146 target=2 parameter2=1 STR_VAR name=c4efPF16 resource=c4efPF16 END

	COPY_EXISTING c4efPF11.spl ~override/c4efPF15.spl~ c4efPF12.spl ~override/c4efPF16.spl~
		LPF DELETE_EFFECT INT_VAR match_opcode=332 END
		LPF DELETE_EFFECT INT_VAR match_opcode=142 END
		LPF ALTER_EFFECT INT_VAR target=1 END

	COPY_EXISTING_REGEXP GLOB ~c4ef[JS]A[123].itm~ override
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode=248 target=1 timing=2 insert_point=999 STR_VAR resource=c4efPF16 END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode=249 target=1 timing=2 insert_point=999 STR_VAR resource=c4efPF16 END
		LPF CLONE_EFFECT STR_VAR match_resource=c4efPF16 resource=c4efPF15 END

	//武器
	LAF CREATE_EFFECT INT_VAR opcode=146 target=1 duration=6 parameter2=1 STR_VAR name=c4efPF11 resource=c4efPF11 END
	LAF CREATE_EFFECT INT_VAR opcode=0 duration=6 probability1=10 parameter1=~-10~ savingthrow=BIT24 STR_VAR name=c4efPF13 END
	COPY_EXISTING_REGEXP ~c4ef[JS]W[12]A\.itm~ override
		// LPF ADD_ITEM_EQEFFECT INT_VAR opcode=146 target=1 timing=2 parameter2=1 STR_VAR resource=c4efPF11 END 
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode=272 target=1 timing=2 parameter1=6 parameter2=3 STR_VAR resource=c4efPF11 END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode=248 target=1 timing=2 STR_VAR resource=c4efPF13 END

	LAF CREATE_EFFECT INT_VAR opcode=146 target=1 duration=6 parameter2=1 STR_VAR name=c4efPF12 resource=c4efPF12 END
	LAF CREATE_EFFECT INT_VAR opcode=0 duration=6 probability1=20 parameter1=~-10~ savingthrow=BIT24 STR_VAR name=c4efPF14 END
	COPY_EXISTING_REGEXP ~c4ef\([JS]W\)2A.itm~ ~override/c4ef\13A.itm~
		LPF ALTER_EFFECT STR_VAR match_resource=c4efPF11 resource=c4efPF12 END
		LPF ALTER_EFFECT STR_VAR match_resource=c4efPF13 resource=c4efPF14 END

	/* 单手 */
	LAF CREATE_EFFECT INT_VAR opcode=146 duration=12 parameter2=1 STR_VAR name=c4efPF21 resource=c4efPF21 END

	LAF CREATE_EMPTY_SPELL STR_VAR name=c4efPF21 END
	COPY_EXISTING c4efPF21.spl override
		LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 savingthrow=BIT24 STR_VAR resource=~%SOURCE_RES%~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=362 target=2 duration=12 parameter1=3 savingthrow=BIT24 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=361 target=2 duration=12 savingthrow=BIT24 STR_VAR resource=c4efPF23 END

	LAF CREATE_EMPTY_SPELL STR_VAR name=c4efPF23 END
	COPY_EXISTING c4efPF23.spl override
		LPF ADD_SPELL_EFFECT INT_VAR opcode=12 target=1 parameter2=2048<<16 dicenumber=2 dicesize=8 savingthrow=BIT24 END

	//精通
	LAF CREATE_EFFECT INT_VAR opcode=146 duration=12 parameter2=1 STR_VAR name=c4efPF22 resource=c4efPF22 END

	LAF CREATE_EMPTY_SPELL STR_VAR name=c4efPF22 END
	COPY_EXISTING c4efPF22.spl override
		LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 savingthrow=BIT24 STR_VAR resource=~%SOURCE_RES%~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=362 target=2 duration=12 parameter1=6 savingthrow=BIT24 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=361 target=2 duration=12 savingthrow=BIT24 STR_VAR resource=c4efPF24 END

	LAF CREATE_EMPTY_SPELL STR_VAR name=c4efPF24 END
	COPY_EXISTING c4efPF24.spl override
		LPF ADD_SPELL_EFFECT INT_VAR opcode=12 target=1 timing=1 parameter2=2048<<16 dicenumber=4 dicesize=8 savingthrow=BIT24 END
		
	//武器
	COPY_EXISTING_REGEXP ~c4ef[JS]W[12]B\.itm~ override
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode=1 target=1 timing=2 parameter1=6 END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode=248 target=1 timing=2 STR_VAR resource=c4efPF21 END

	COPY_EXISTING_REGEXP ~c4ef\([JS]W\)2B.itm~ ~override/c4ef\13B.itm~
		LPF ALTER_EFFECT STR_VAR match_resource=c4efPF21 resource=c4efPF22 END
		
	/* 双持 */
	LAF CREATE_EFFECT INT_VAR opcode=0 duration=12 parameter1=~-1~ savingthrow=BIT24 STR_VAR name=c4efPF31 END
	LAF CREATE_EFFECT INT_VAR opcode=278 duration=12 parameter1=~-1~ savingthrow=BIT24 STR_VAR name=c4efPF32 END

	//精通
	LAF CREATE_EFFECT INT_VAR opcode=0 duration=18 parameter1=~-1~ savingthrow=BIT24 STR_VAR name=c4efPF33 END
	LAF CREATE_EFFECT INT_VAR opcode=278 duration=18 parameter1=~-1~ savingthrow=BIT24 STR_VAR name=c4efPF34 END
		
	//武器
	COPY_EXISTING_REGEXP ~c4ef[JS]W[12][CD]\.itm~ override
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode=248 target=1 timing=2 STR_VAR resource=c4efPF32 END
		LPF CLONE_EFFECT STR_VAR match_resource=c4efPF32 resource=c4efPF31 END

	COPY_EXISTING_REGEXP ~c4ef\([JS]W\)2\([CD]\).itm~ ~override/c4ef\13\2.itm~
		LPF ALTER_EFFECT STR_VAR match_resource=c4efPF31 resource=c4efPF33 END
		LPF ALTER_EFFECT STR_VAR match_resource=c4efPF32 resource=c4efPF34 END

	/* **************************************************** */
	/* 文本 */
	COPY_EXISTING c4efJA1.itm override
		SAY NAME2	@1101
		SAY DESC	@1102
		
	COPY_EXISTING c4efJA2.itm override
		SAY NAME2	@1103
		SAY DESC	@1104
		
	COPY_EXISTING c4efJA3.itm override
		SAY NAME2	@1105
		SAY DESC	@1106
		
	COPY_EXISTING c4efSA1.itm override
		SAY NAME2	@1201
		SAY DESC	@1202
		
	COPY_EXISTING c4efSA2.itm override
		SAY NAME2	@1203
		SAY DESC	@1204
		
	OUTER_SET $STYPE(J)=1100
	OUTER_SET $STYPE(S)=1200

	OUTER_SET $SNAME(A)=0
	OUTER_SET $SNAME(B)=0
	OUTER_SET $SNAME(C)=3
	OUTER_SET $SNAME(D)=5

	OUTER_SET $SDESC(A)=1
	OUTER_SET $SDESC(B)=2
	OUTER_SET $SDESC(C)=4
	OUTER_SET $SDESC(D)=6

	ACTION_PHP_EACH STYPE AS type => n1 BEGIN
		OUTER_FOR (i=1;i<4;++i) BEGIN
			ACTION_PHP_EACH SNAME AS name => n2 BEGIN
				OUTER_SET name_tra = n1 + i*10 + n2
				COPY_EXISTING ~c4ef%type%W%i%%name%.itm~ override
					strref = RESOLVE_STR_REF ((AT "%name_tra%"))
					WRITE_LONG 0xc strref
			END
			ACTION_PHP_EACH SDESC AS name => n2 BEGIN
				OUTER_SET desc_tra = n1 + i*10 + n2
				COPY_EXISTING ~c4ef%type%W%i%%name%.itm~ override
					strref = RESOLVE_STR_REF ((AT "%desc_tra%"))
					WRITE_LONG 0x54 strref
			END
		END
	END

	/* **************************************************** */
	/* 护甲生成/替换 */
	//绝地A
	COPY ~%mod_root%/itm/c4efAmor.spl~ ~override/c4efJA1.spl~
		LPF ALTER_EFFECT INT_VAR match_opcode=143 STR_VAR resource=c4efJA1 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF ((AT 1010)) END

	//绝地B
	COPY ~%mod_root%/itm/c4efAmor.spl~ ~override/c4efJA2.spl~
		LPF ALTER_EFFECT INT_VAR match_opcode=143 STR_VAR resource=c4efJA2 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF ((AT 1010)) END

	//绝地CD
	COPY ~%mod_root%/itm/c4efAmor.spl~ ~override/c4efJA3.spl~
		LPF ALTER_EFFECT INT_VAR match_opcode=143 STR_VAR resource=c4efJA3 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF ((AT 1010)) END

	//西斯A
	COPY ~%mod_root%/itm/c4efAmor.spl~ ~override/c4efSA1.spl~
		LPF ALTER_EFFECT INT_VAR match_opcode=143 STR_VAR resource=c4efSA1 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF ((AT 1020)) END

	//西斯B
	COPY ~%mod_root%/itm/c4efAmor.spl~ ~override/c4efSA2.spl~
		LPF ALTER_EFFECT INT_VAR match_opcode=143 STR_VAR resource=c4efSA2 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF ((AT 1020)) END

	//flag
	COPY_EXISTING_REGEXP GLOB ~c4ef\([JS]A[1-3]\).spl~ ~override/c4ef\1R.spl~
		WRITE_LONG 0x18 (BIT14 + BIT15 + BIT24 +  BIT25)
		
	//丢失恢复
	COPY_EXISTING_REGEXP GLOB ~c4ef\([JS]A[1-3]\).spl~ ~override/c4ef\1R.spl~
		LPF DELETE_EFFECT INT_VAR match_opcode=139 END

	/* **************************************************** */
	//(伪)精通一级光剑
	COPY_EXISTING_REGEXP GLOB ~c4ef\([JS]W\)1\([A-D]\).itm~ ~override/c4ef\14\2.itm~
		
	//表格中初始化
	ACTION_PHP_EACH kit_armor AS kit => spell BEGIN	
		COPY_EXISTING ~c4%kit%.2da~ override
			TO_UPPER spell
			COUNT_2DA_COLS cols
			SET_2DA_ENTRY 1 1 cols ~AP_%spell%~
			PRETTY_PRINT_2DA
	END

	/* **************************************************** */
	// 剑术专攻
	INCLUDE ~%LIBS%/sabre_checks.tph~

END