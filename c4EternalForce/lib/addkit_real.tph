
/* 宗派定义和切换：
	表格：
		0. 1级，基础被动，#321移除所有宗派技能0
		1. 1级，护甲
		2. 1级，光剑
		3. 1级，#171#172：添加应有，移除不应有
		4. 感应等其他技能
		6. 西斯的原力凝聚
		7. 法术位技能，必须在获得该等级时添加不然无效
		8. 每等级：移除所有原版神术，延迟1秒再移除
	脚本：
		1. 等级/技能晋升
		2. 侵蚀度>=30根据炼金术变量决定堕落目标
		3. 堕落后声望>=18：计时器启动变量=1，ActionOverride计时变量=36000 5天
		4. 放黑暗技能计时器重置法术：计时变量重置
		5. 声望<18：启动变量=0，计时变量=50天360000或可能的无限大
		6. GlobalTimerExpired：根据大师和启迪变量决定救赎目标，计时启动变量=0，计时变量==50天360000或可能的无限大
		7. 已学高阶恢复
*/

/* 基础被动 */
//移除其他职业
COPY_EXISTING c4ef2JA0.spl override c4ef2SA0.spl override
	PATCH_FOR_EACH kit IN JA JB JC JD SA SB BEGIN
		LPF CLONE_EFFECT INT_VAR match_opcode=144 opcode=321 timing=0 parameter2=0 STR_VAR resource=~c4ef2%kit%0~ END
	END
	LPF CLONE_EFFECT INT_VAR match_opcode=144 opcode=321 timing=0 parameter2=0 STR_VAR resource=~c4ef2SA1~ END
	COUNT_2DA_COLS cols
	
//版本标记
LAF CREATE_EFFECT INT_VAR opcode=187 timing=1 parameter1=c4EternalForceVer STR_VAR name=c4efVer variable=c4EternalForceVer END
COPY_EXISTING c4ef2JA0.spl override c4ef2SA0.spl override
	LPF CLONE_EFFECT INT_VAR match_opcode=144 opcode=177 timing=1 parameter2=2 STR_VAR resource=c4efVer END
	
//禁远程+投掷惩罚
LAF CREATE_EFFECT INT_VAR opcode=362 target=1 timing=0 duration=600 parameter1=18 STR_VAR name=c4efNoRW END
COPY_EXISTING c4ef2JA0.spl override c4ef2SA0.spl override
	LPF CLONE_EFFECT INT_VAR match_opcode=144 opcode=249 parameter2=0 STR_VAR resource=c4efNoRW END
	PATCH_FOR_EACH type IN 15 18 24 27 BEGIN
		LPF CLONE_EFFECT INT_VAR match_opcode=144 opcode=181 parameter1=type parameter2=0 special= RESOLVE_STR_REF ((AT 1001)) END
	END
	
//绝地武士
COPY_EXISTING c4ef2JA0.spl ~override/c4ef2JB0.spl~
	//移除原力共振效果
	LPF CLONE_EFFECT INT_VAR match_opcode=144 opcode=321 timing=0 parameter2=0 STR_VAR resource=~c4ef706~ END
	//联结
	LPF CLONE_EFFECT INT_VAR match_opcode=144 opcode=62 parameter1=1 parameter2=BIT0+BIT1+BIT2+BIT3 END
	
//绝地大师
COPY_EXISTING c4ef2JA0.spl ~override/c4ef2JC0.spl~
	LPF CLONE_EFFECT INT_VAR match_opcode=144 opcode=321 timing=0 parameter2=0 STR_VAR resource=~c4ef706~ END
	//高等联结
	LPF CLONE_EFFECT INT_VAR match_opcode=144 opcode=62 parameter1=2 parameter2=BIT0+BIT1+BIT2+BIT3 END
	
COPY_EXISTING c4ef2JC0.spl ~override/c4ef2JD0.spl~

//西斯大君
COPY_EXISTING c4ef2SA0.spl ~override/c4ef2SB0.spl~
	//汲取
	LPF CLONE_EFFECT INT_VAR match_opcode=144 opcode=62 parameter1=2 parameter2=BIT0+BIT1+BIT2+BIT3 END
	
/* 添加/移除神术 */
//学徒（没鸟用）
LAF CREATE_EMPTY_SPELL STR_VAR name=c4ef2FJA END
COPY_EXISTING c4ef2FJA.spl override
	PHP_EACH skills_align AS file => type BEGIN
		SET file_match=0
		PATCH_FOR_EACH what IN 0 1 2 11 12 BEGIN
			SET file_match = (what=type) ? 1 : file_match
		END
		PATCH_IF file_match BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
		END ELSE LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
	END

COPY_EXISTING c4JediA.2da override
	COUNT_2DA_COLS cols
	SET_2DA_ENTRY 0 1 cols ~AP_C4EF2JA0~
	SET_2DA_ENTRY 3 1 cols ~AP_C4EF2FJA~
	
//绝地武士
LAF CREATE_EMPTY_SPELL STR_VAR name=c4ef2FJB END
COPY_EXISTING c4ef2FJB.spl override
	PHP_EACH skills_align AS file => type BEGIN
		SET file_match=0
		PATCH_FOR_EACH what IN 0 1 2 11 12 BEGIN
			SET file_match = (what=type) ? 1 : file_match
		END
		PATCH_IF file_match BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
		END ELSE LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
	END

COPY_EXISTING c4JediB.2da override
	COUNT_2DA_COLS cols
	SET_2DA_ENTRY 0 1 cols ~AP_C4EF2JB0~
	SET_2DA_ENTRY 3 1 cols ~AP_C4EF2FJB~
	
//绝地大师
LAF CREATE_EMPTY_SPELL STR_VAR name=c4ef2FJC END
COPY_EXISTING c4ef2FJC.spl override
	PHP_EACH skills_align AS file => type BEGIN
		SET file_match=0
		PATCH_FOR_EACH what IN 0 1 2 BEGIN
			SET file_match = (what=type) ? 1 : file_match
		END
		PATCH_IF file_match BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
		END ELSE LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
	END

COPY_EXISTING c4JediC.2da override
	COUNT_2DA_COLS cols
	SET_2DA_ENTRY 0 1 cols ~AP_C4EF2JC0~
	SET_2DA_ENTRY 3 1 cols ~AP_C4EF2FJC~
	
//绝地大师+启迪
LAF CREATE_EMPTY_SPELL STR_VAR name=c4ef2FJD END
COPY_EXISTING c4ef2FJD.spl override
	PHP_EACH skills_align AS file => type BEGIN
		SET file_match=0
		PATCH_FOR_EACH what IN 0 1 3 BEGIN
			SET file_match = (what=type) ? 1 : file_match
		END
		PATCH_IF file_match BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
		END ELSE LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
	END

COPY_EXISTING c4JediD.2da override
	COUNT_2DA_COLS cols
	SET_2DA_ENTRY 0 1 cols ~AP_C4EF2JD0~
	SET_2DA_ENTRY 3 1 cols ~AP_C4EF2FJD~
	
//西斯武士
LAF CREATE_EMPTY_SPELL STR_VAR name=c4ef2FSA END
COPY_EXISTING c4ef2FSA.spl override
	PHP_EACH skills_align AS file => type BEGIN
		SET file_match=0
		PATCH_FOR_EACH what IN 0 1 2 11 12 BEGIN
			SET file_match = (what=type) ? 1 : file_match
		END
		PATCH_IF file_match BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
		END ELSE LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
	END

COPY_EXISTING c4SithA.2da override
	COUNT_2DA_COLS cols
	SET_2DA_ENTRY 0 1 cols ~AP_C4EF2SA0~
	SET_2DA_ENTRY 3 1 cols ~AP_C4EF2FSA~
	
//西斯大君
LAF CREATE_EMPTY_SPELL STR_VAR name=c4ef2FSB END
COPY_EXISTING c4ef2FSB.spl override
	PHP_EACH skills_align AS file => type BEGIN
		SET file_match=0
		PATCH_FOR_EACH what IN 0 2 11 13 BEGIN
			SET file_match = (what=type) ? 1 : file_match
		END
		PATCH_IF file_match BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
		END ELSE LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=9 STR_VAR resource=~c4ef%file%~ END
	END

COPY_EXISTING c4SithB.2da override
	COUNT_2DA_COLS cols
	SET_2DA_ENTRY 0 1 cols ~AP_C4EF2SB0~
	SET_2DA_ENTRY 3 1 cols ~AP_C4EF2FSB~

/* 主动技能 */
//绝地无启迪
COPY_EXISTING_REGEXP GLOB ~c4Jedi[ABC].2da~ override
	COUNT_2DA_COLS cols
	//感应
	SET_2DA_ENTRY_LATER new 4 1 ~GA_C4EF001~
	FOR (i=5;i<cols;i+=5) BEGIN
		SET_2DA_ENTRY_LATER new 4 i ~GA_C4EF001~
	END
	//治疗
	FOR (i=4;i<cols;i+=4) BEGIN
		SET_2DA_ENTRY_LATER new 5 i ~GA_C4EF003~
	END
	SET_2DA_ENTRIES_NOW new cols
	
//启迪
COPY_EXISTING c4JediD.2da override
	COUNT_2DA_COLS cols
	//感应
	SET_2DA_ENTRY_LATER new 4 1 ~GA_C4EF002~
	FOR (i=5;i<cols;i+=5) BEGIN
		SET_2DA_ENTRY_LATER new 4 i ~GA_C4EF002~
	END
	//救援
	FOR (i=4;i<cols;i+=4) BEGIN
		SET_2DA_ENTRY_LATER new 5 i ~GA_C4EF004~
	END
	SET_2DA_ENTRIES_NOW new cols
	
//西斯
COPY_EXISTING_REGEXP GLOB ~c4Sith[AB].2da~ override
	COUNT_2DA_COLS cols
	//感应
	SET_2DA_ENTRY_LATER new 4 1 ~GA_C4EF001~
	FOR (i=5;i<cols;i+=5) BEGIN
		SET_2DA_ENTRY_LATER new 4 i ~GA_C4EF001~
	END
	//结界
	FOR (i=4;i<cols;i+=4) BEGIN
		SET_2DA_ENTRY_LATER new 5 i ~GA_C4EF005~
	END
	//凝聚
	FOR (i=9;i<34;i+=4) BEGIN
		SET_2DA_ENTRY_LATER new 6 i ~AP_C4EFSA1~
	END
	SET_2DA_ENTRIES_NOW new cols

/* 添加法术位 */
//+1
LAF CREATE_EMPTY_SPELL STR_VAR name=c4efExS1 END
COPY_EXISTING c4efExS1.spl override
	LPF ADD_SPELL_EFFECT INT_VAR opcode=62 timing=9 parameter1=1 parameter2=BIT0+BIT1+BIT2+BIT3 END
	LPF CLONE_EFFECT INT_VAR opcode=321 timing=0 parameter1=0 parameter2=0 STR_VAR resource=~%SOURCE_RES%~ END

COPY_EXISTING c4JediB.2da override
	COUNT_2DA_COLS cols
	FOR (i=9;i<16;i+=2) BEGIN
		SET_2DA_ENTRY_LATER new 7 i ~AP_C4EFEXS1~
	END
	SET_2DA_ENTRIES_NOW new cols

//+2
LAF CREATE_EMPTY_SPELL STR_VAR name=c4efExS2 END
COPY_EXISTING c4efExS2.spl override
	LPF ADD_SPELL_EFFECT INT_VAR opcode=62 timing=9 parameter1=2 parameter2=BIT0+BIT1+BIT2+BIT3 END
	LPF CLONE_EFFECT INT_VAR opcode=321 timing=0 parameter1=0 parameter2=0 STR_VAR resource=~%SOURCE_RES%~ END

COPY_EXISTING c4JediC.2da override c4JediD.2da override c4JediB.2da override
	COUNT_2DA_COLS cols
	FOR (i=9;i<16;i+=2) BEGIN
		SET_2DA_ENTRY_LATER new 7 i ~AP_C4EFEXS2~
	END
	SET_2DA_ENTRIES_NOW new cols


/* 移除原版神术 */
COPY_EXISTING_REGEXP - GLOB ~SPPR[1-4][0-9]+\.spl~ override
	SET $ori_priest_spl(~%SOURCE_RES%~)=1

LAF CREATE_EMPTY_SPELL STR_VAR name=c4efORIG END
COPY_EXISTING c4efORIG.spl override
	PHP_EACH ori_priest_spl AS file => int BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=9 STR_VAR resource=~%file%~ END
	END
	PHP_EACH ori_priest_spl AS file => int BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=4 duration=1 STR_VAR resource=~%file%~ END
	END
	
COPY_EXISTING_REGEXP GLOB ~c4\(Jedi\|Sith\)[ABCD].2da~ override
	COUNT_2DA_COLS cols
	FOR (i=1;i<cols;++i) BEGIN
		SET_2DA_ENTRY_LATER ori 8 i ~AP_C4EFORIG~
	END
	// PATCH_IF NOT ~%SOURCE_RES%~ STR_EQ c4JediA BEGIN
		SET_2DA_ENTRIES_NOW ori cols
	// END
	PRETTY_PRINT_2DA

