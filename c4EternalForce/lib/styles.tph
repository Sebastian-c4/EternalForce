

/* 清除切换剑式技能 */
LAF CREATE_EMPTY_SPELL STR_VAR name=c4efJj0R END
COPY_EXISTING c4efJj0R.spl override
	WRITE_LONG 0x18 (BIT14 + BIT15 + BIT24 +  BIT25)
	FOR (i=1;i<8;++i) BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=2 timing=1 savingthrow=BIT24 STR_VAR resource=~c4efJj%i%0~ END
	END
	LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 timing=1 parameter2=2 savingthrow=BIT24 STR_VAR resource=~c4efJj0V~ END
LAF CREATE_EFFECT INT_VAR opcode=187 timing=9 parameter1=0 STR_VAR name=~c4efJj0V~ variable=~c4ef_Jedi_Style~ END

OUTER_FOR (i=1;i<8;++i) BEGIN
	//恢复
	COPY_EXISTING c4efJj0R.spl ~override/c4efJj%i%R.spl~
		LPF ALTER_EFFECT INT_VAR match_opcode=177 opcode=171 parameter2=0 STR_VAR resource=~c4efJj%i%0~ insert=last END
		
	//切换母技能
	COPY ~%MOD_FOLDER%/styles/c4efJj00.spl~ ~override/c4efJj%i%0.spl~
		SAY 0x8 @2000
		desc_tra = 2000+i
		strref = RESOLVE_STR_REF ((AT "%desc_tra%"))
		WRITE_LONG 0x50 strref
		WRITE_LONG 0x18 (BIT14 + BIT15 + BIT24 +  BIT25)
		WRITE_ASCII 0x3a ~c4efJj0B~
		LPF ALTER_SPELL_HEADER STR_VAR icon=~c4efJj0B~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=214 target=1 timing=1 savingthrow=BIT24 STR_VAR resource=~c4efJj%i%~ END
		
	//切换子技能
	COPY ~%MOD_FOLDER%/styles/c4efJj0-.spl~ ~override/c4efJj%i%-.spl~
		name_tra = 2010+i
		strref = RESOLVE_STR_REF ((AT "%name_tra%"))
		WRITE_LONG 0x8 strref
		desc_tra = 2101+i*10+1
		strref = RESOLVE_STR_REF ((AT "%desc_tra%"))
		WRITE_LONG 0x50 strref
		WRITE_LONG 0x18 (BIT14 + BIT15 + BIT24 +  BIT25)
		WRITE_ASCII 0x3a ~c4efJj%i%B~
		LPF ALTER_SPELL_HEADER STR_VAR icon=~c4efJj%i%B~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=143 target=2 timing=9 parameter1=6 savingthrow=BIT24 STR_VAR resource=~c4efJj%i%~ END
		
	//当前剑式标记
	ACTION_IF i<7 BEGIN
		LAF CREATE_EFFECT INT_VAR opcode=187 timing=1 parameter1=i STR_VAR name=~c4efJj%i%V~ variable=~c4EF_Jedi_Style~ END
		COPY_EXISTING ~c4efJj%i%-.spl~ override
			LPF CLONE_EFFECT INT_VAR match_opcode=143 opcode=177 parameter1=0 parameter2=2 STR_VAR resource=~c4efJj%i%V~ END
	END
		
	//帽子
	COPY_EXISTING ~c4efJj%i%.itm~ override
		name_tra = 2101+i*10
		strref = RESOLVE_STR_REF ((AT "%name_tra%"))
		WRITE_LONG 0xc strref
		desc_tra = 2101+i*10+1
		strref = RESOLVE_STR_REF ((AT "%desc_tra%"))
		WRITE_LONG 0x54 strref
END

/* 二型 */
LAF CREATE_EFFECT INT_VAR opcode=1 target=1 duration=6 parameter1=6 probability1=15 savingthrow=BIT24 STR_VAR name=c4efJj2A END

COPY_EXISTING ~c4efJj2.itm~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 parameter1=0 STR_VAR resource=c4efJj2A END

/* 三型 */
ADD_PROJECTILE ~%PROJECTILE%/c4efJj3A.pro~
LAF CREATE_EFFECT INT_VAR opcode=146 target=1 timing=1 parameter2=1 savingthrow=BIT24 STR_VAR name=c4efJj3A resource=c4efJj3A END

COPY_EXISTING c4efJj3A.spl override
	WRITE_LONG 0x18 (BIT10 + BIT14 + BIT15 + BIT24 +  BIT25)
	LPF ALTER_SPELL_HEADER INT_VAR projectile=c4efJj3A END
	LPF ALTER_EFFECT INT_VAR match_opcode=12 savingthrow=BIT24 END
	LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_multiplier=2 END
	LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=12 multi_parameter1=1 END

/* 四型 */
ADD_SECTYPE ~c4EternalForce_Form4~ @2144
COPY_EXISTING ~c4efJj4Z.spl~ override
	WRITE_LONG 0x18 (BIT14 + BIT15 + BIT24 +  BIT25)
	LPF ALTER_EFFECT INT_VAR match_opcode=221 parameter2=c4EternalForce_Form4	END

COPY_EXISTING_REGEXP ~c4efJj4[ABC].spl~ override
	WRITE_LONG 0x18 (BIT14 + BIT15 + BIT24 +  BIT25)
	WRITE_BYTE 0x27 c4EternalForce_Form4
	
//击晕
LAF CREATE_EFFECT INT_VAR opcode=39 duration=3 probability1=33 savingthrow=BIT24 STR_VAR name=c4efJj4A END

COPY_EXISTING_REGEXP ~c4efJj4[AB].spl~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=1 opcode=248 parameter1=0 STR_VAR resource=c4efJj4A END
	
//生效标记和清除
LAF CREATE_EFFECT INT_VAR opcode=187 timing=1 parameter1=1 savingthrow=BIT24 STR_VAR name=c4efJj4Y variable=~c4EF_Jedi_Style_Form4~ END
LAF CREATE_EFFECT INT_VAR opcode=187 timing=1 parameter1=0 savingthrow=BIT24 STR_VAR name=c4efJj4Z variable=~c4EF_Jedi_Style_Form4~ END

COPY_EXISTING ~c4efJj4A.spl~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=1 opcode=177 parameter1=0 parameter2=2 STR_VAR resource=c4efJj4Y END
	LPF CLONE_EFFECT INT_VAR match_opcode=1 opcode=139 parameter1= RESOLVE_STR_REF ((AT 2143)) END

COPY_EXISTING ~c4efJj4Z.spl~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=221 opcode=177 parameter2=2 STR_VAR resource=c4efJj4Z END
	
COPY_EXISTING_REGEXP ~c4efJj4.*\.spl~ override
	LPF ALTER_EFFECT INT_VAR savingthrow=BIT24 END	

/* 五型 */
ADD_SECTYPE ~c4EternalForce_Form5~ @2154
COPY_EXISTING ~c4efJj5Z.spl~ override
	WRITE_LONG 0x18 (BIT14 + BIT15 + BIT24 +  BIT25)
	LPF ALTER_EFFECT INT_VAR match_opcode=221 parameter2=c4EternalForce_Form5	END

COPY_EXISTING_REGEXP ~c4efJj5[ABT].spl~ override
	WRITE_LONG 0x18 (BIT14 + BIT15 + BIT24 +  BIT25)
	WRITE_BYTE 0x27 c4EternalForce_Form5
	
//反击
LAF CREATE_EFFECT INT_VAR opcode=146 target=1 duration=2 parameter2=1  probability1=20 savingthrow=BIT24 STR_VAR name=c4efJj5T resource=c4efJj5T END

COPY_EXISTING c4efJj5A.spl override
	LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 parameter1=0 STR_VAR resource=c4efJj5T END

COPY_EXISTING c4efJj5T.spl override
	LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF ((AT 2153)) END
	
//防护和移除加速
LAM ~haste_effect~
COPY_EXISTING c4efJj5.itm override
	LPF CLONE_EFFECT INT_VAR match_opcode=325 opcode=101 parameter1=0 parameter2=16 STR_VAR insert=last END
	LPF CLONE_EFFECT INT_VAR match_opcode=325 opcode=337 parameter1=0 parameter2=16 STR_VAR insert=last END
	LPF CLONE_EFFECT INT_VAR match_opcode=325 opcode=337 parameter1=1 parameter2=16 STR_VAR insert=last END
	PHP_EACH haste_spl AS file => int BEGIN
		LPF CLONE_EFFECT INT_VAR match_opcode=325 opcode=321 timing=0 parameter2=0 STR_VAR resource=~%file%~ insert=last END
		LPF CLONE_EFFECT INT_VAR match_opcode=325 opcode=206 parameter1=4742 parameter2=0 STR_VAR resource=~%file%~ insert=last END
	END
	PHP_EACH haste_string AS parameter1 => int BEGIN
		LPF CLONE_EFFECT INT_VAR match_opcode=325 opcode=267 parameter1 parameter2=0 STR_VAR insert=last END
	END
	PHP_EACH haste_icon AS parameter2 => int BEGIN
		LPF CLONE_EFFECT INT_VAR match_opcode=325 opcode=169 parameter1=0 parameter2 STR_VAR insert=last END
		LPF CLONE_EFFECT INT_VAR match_opcode=325 opcode=240 parameter1=0 parameter2 STR_VAR insert=last END
	END
	
//生效标记和清除
LAF CREATE_EFFECT INT_VAR opcode=187 timing=1 parameter1=1 savingthrow=BIT24 STR_VAR name=c4efJj5Y variable=~c4EF_Jedi_Style_Form5~ END
LAF CREATE_EFFECT INT_VAR opcode=187 timing=1 parameter1=0 savingthrow=BIT24 STR_VAR name=c4efJj5Z variable=~c4EF_Jedi_Style_Form5~ END

COPY_EXISTING ~c4efJj5A.spl~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=177 parameter1=0 parameter2=2 STR_VAR resource=c4efJj5Y END

COPY_EXISTING ~c4efJj5Z.spl~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=221 opcode=177 parameter2=2 STR_VAR resource=c4efJj5Z END

COPY_EXISTING_REGEXP ~c4efJj5[ABTZ]\.spl~ override
	LPF ALTER_EFFECT INT_VAR savingthrow=BIT24 END

/* 六型 */
ACTION_FOR_EACH file IN sppr409 sppr508 BEGIN	
	COPY_EXISTING - ~%file%.spl~ override
		FOR (i=2;i<51;i+=1) BEGIN
			LPF DELETE_SPELL_HEADER INT_VAR header_type=~-1~ min_level=i END
		END
		GET_OFFSET_ARRAY header SPL_V10_HEADERS
		PHP_EACH header AS int => index BEGIN
			GET_OFFSET_ARRAY2 effect index SPL_V10_HEAD_EFFECTS 
			PHP_EACH effect AS int => offset BEGIN
				PATCH_FOR_EACH target IN 101 169 142 328 282 267 233 206 296 BEGIN
					READ_SHORT offset opcode
					PATCH_IF opcode = target BEGIN
						READ_LONG offset+0x4 parameter1
						READ_LONG offset+0x8 parameter2
						READ_LONG offset+0x2c special
						READ_ASCII offset+0x14 resource
						INNER_ACTION BEGIN
							COPY_EXISTING c4efJj6.itm override
								LPF DELETE_EFFECT
									INT_VAR
										match_opcode=opcode
										match_parameter1=parameter1
										match_parameter2=parameter2
										match_special=special
									STR_VAR
										match_resource=resource
								END
								LPF CLONE_EFFECT
									INT_VAR
										match_opcode=6
										opcode parameter1 parameter2 special
									STR_VAR
										resource insert=last
								END
						END
					END
				END
			END
		END
END

//迅捷：其他型免疫迅捷效果，在技能中添加
COPY_EXISTING c4efJjIA.spl override
	// SAY NAME1 @2163
	LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF ((AT 2163)) END
	
COPY_EXISTING_REGEXP ~c4efJj[123457]\.itm~ override ~c4efSj[12]\.itm~ override
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode=206 target=1 timing=2 STR_VAR resource=c4efJjIA END

/* 七型 */
//免疫切换
COPY_EXISTING c4efJj7.itm override
	FOR (i=1;i<7;++i) BEGIN
		LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=206 parameter1= RESOLVE_STR_REF ((AT 2173)) STR_VAR resource=~c4efJj%i%-~ insert=last END
	END

//侵蚀
COPY_EXISTING c4efJj7-.spl override
	LPF CLONE_EFFECT INT_VAR match_opcode=143 opcode=177 parameter1=0 parameter2=2 STR_VAR resource=~c4efES5~ END

//击晕
LAF CREATE_EFFECT INT_VAR opcode=39 duration=3 probability1=33 savingthrow=BIT24 STR_VAR name=c4efJj7A END
	
//解除战斗防护
LAF CREATE_EFFECT INT_VAR opcode=230 timing=1 parameter1=9 parameter2=7 probability1=66 probability2=34 savingthrow=BIT24 STR_VAR name=c4efJj7B END
	
//解除法术防护
LAF CREATE_EFFECT INT_VAR opcode=230 timing=1 parameter1=9 parameter2=1 probability1=100 probability2=67 savingthrow=BIT24 STR_VAR name=c4efJj7C END

ACTION_FOR_EACH type IN A B C BEGIN
	COPY_EXISTING c4efJj7.itm override
		LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 parameter1=0 STR_VAR resource=~c4efJj7%type%~ END
END

//7型标记
LAF CREATE_EFFECT INT_VAR opcode=187 timing=1 parameter1=1 savingthrow=BIT24 STR_VAR name=~c4efJj7Y~ variable=~c4EF_Jedi_Style_Form7~ END

LAF CREATE_EMPTY_SPELL STR_VAR name=c4efJj7Y END
COPY_EXISTING c4efJj7Y.spl override
	LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 timing=1 parameter2=2 STR_VAR resource=c4efJj7Y END
	
//恢复其他剑式
LAF CREATE_EFFECT INT_VAR opcode=187 timing=1 parameter1=0 savingthrow=BIT24 STR_VAR name=~c4efJj7Z~ variable=~c4EF_Jedi_Style_Form7~ END

OUTER_FOR (i=1;i<7;++i) BEGIN
	COPY_EXISTING ~c4efJj%i%-.spl~ ~override/c4efJj7%i%.spl~
		WRITE_LONG 8 ~-1~
		LPF CLONE_EFFECT INT_VAR match_opcode=143 opcode=215 parameter1=0 parameter2=1 timing=0 duration=1 STR_VAR resource=SPPOWWRD END
		LPF CLONE_EFFECT INT_VAR match_opcode=143 opcode=177 parameter1=0 parameter2=2 STR_VAR resource=c4efJj7Z END
		SET string=2173+i
		LPF CLONE_EFFECT INT_VAR match_opcode=143 opcode=139 parameter1= RESOLVE_STR_REF ((AT %string%)) END
END

//堕落时重置7型变量
COPY_EXISTING_REGEXP GLOB ~c4efSj[12]-.spl~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=143 opcode=177 parameter1=0 parameter2=2 STR_VAR resource=c4efJj7Z END
	


/* 西斯武士剑式 */
//吸血
LAF CREATE_EFFECT INT_VAR opcode=12 target=2 timing=1 parameter2=4096<<16 dicesize=2 dicenumber=1 savingthrow=BIT24 special=BIT0 STR_VAR name=c4efSj1A END

COPY_EXISTING ~c4efSj1.itm~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 parameter1=0 STR_VAR resource=c4efSj1A END
	SAY NAME2 @2211
	SAY DESC @2212

//加速
LAF CREATE_EFFECT INT_VAR opcode=1 target=1 duration=6 parameter1=6 probability1=15 savingthrow=BIT24 STR_VAR name=c4efSj1B END

COPY_EXISTING ~c4efSj1.itm~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 parameter1=0 STR_VAR resource=c4efSj1B END

/* 西斯大君剑式 */
//吸血
LAF CREATE_EFFECT INT_VAR opcode=12 target=2 timing=1 parameter2=4096<<16 dicesize=4 dicenumber=1 savingthrow=BIT24 special=BIT0 STR_VAR name=c4efSj2A END

COPY_EXISTING ~c4efSj2.itm~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 parameter1=0 STR_VAR resource=c4efSj2A END
	SAY NAME2 @2221
	SAY DESC @2222

//加速
COPY_EXISTING ~c4efSj2.itm~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 parameter1=0 STR_VAR resource=c4efSj1B END

//触发重击
LAF CREATE_EFFECT INT_VAR opcode=146 target=1 parameter2=1 probability1=30 probability2=16 savingthrow=BIT24 STR_VAR name=c4efSj2C resource=c4efSj2C END

COPY_EXISTING ~c4efSj2.itm~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 parameter1=0 STR_VAR resource=c4efSj2C END

//清除
ADD_SECTYPE ~c4EternalForce_Sith2~ @2223
COPY_EXISTING ~c4efSj2Z.spl~ override
	WRITE_LONG 0x18 (BIT14 + BIT15 + BIT24 +  BIT25)
	LPF ALTER_EFFECT INT_VAR match_opcode=221 parameter2=c4EternalForce_Sith2	END
	
//叠加
LAF CREATE_EFFECT INT_VAR opcode=146 target=1 parameter2=1 probability1=55  probability2=31 savingthrow=BIT24 STR_VAR name=c4efSj20 resource=c4efSj20 END

COPY_EXISTING ~c4efSj2.itm~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 parameter1=0 STR_VAR resource=c4efSj2H END
	
OUTER_FOR (i=1;i<11;++i) BEGIN
	OUTER_PATCH_SAVE now x BEGIN
		WRITE_BYTE 0 i+71
	END
	OUTER_PATCH_SAVE next x BEGIN
		WRITE_BYTE 0 i+72
	END
	
	COPY_EXISTING ~c4efSj20.eff~ ~override/c4efSj2%now%.eff~
		WRITE_ASCIIE 0x30 ~c4efSj2%now%~

	COPY ~%MOD_FOLDER%/styles/c4efSj20.spl~ ~override/c4efSj2%now%.spl~
		WRITE_LONG 0x18 (BIT14 + BIT15 + BIT24 +  BIT25)
		WRITE_BYTE 0x27 c4EternalForce_Sith2
		LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~c4efSj2%now%~ END
		PATCH_IF i<10 BEGIN
			LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~c4efSj2%next%~ END
		END ELSE LPF DELETE_EFFECT INT_VAR match_opcode=248 END
END
	
//生效标记和清除
LAF CREATE_EFFECT INT_VAR opcode=187 timing=1 parameter1=1 savingthrow=BIT24 STR_VAR name=c4efSj2Y variable=~c4EF_Sith_Style_2~ END
LAF CREATE_EFFECT INT_VAR opcode=187 timing=1 parameter1=0 savingthrow=BIT24 STR_VAR name=c4efSj2Z variable=~c4EF_Sith_Style_2~ END

COPY_EXISTING_REGEXP ~c4efSj2[H-Q].spl~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=73 opcode=177 parameter1=0 parameter2=2  STR_VAR resource=c4efSj2Y END

COPY_EXISTING ~c4efSj2Z.spl~ override
	LPF CLONE_EFFECT INT_VAR match_opcode=221 opcode=177 parameter2=2 STR_VAR resource=c4efSj2Z END
	
/* 获得西斯剑式 */
// COPY_EXISTING ~c4SithA.2da~ override
	// COUNT_2DA_COLS cols
	// SET_2DA_ENTRY 9 1 cols ~AP_c4efSj1-~
	// PRETTY_PRINT_2DA
// COPY_EXISTING ~c4SithB.2da~ override
	// COUNT_2DA_COLS cols
	// SET_2DA_ENTRY 9 1 cols ~AP_c4efSj2-~
	// PRETTY_PRINT_2DA
